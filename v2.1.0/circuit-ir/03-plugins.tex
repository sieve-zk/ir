
\section{Plugins}
\label{sec:plugins}

\subsection{Motivation}

% Why we need plugins
In the previous section, we describe the Circuit-IR which contains the core IR functionalities.
In this intermediate representation, we would like to add some (complex) features (e.g. RAM operations).
Unfortunately, each update in the basic syntax forces frontends and backends to update their IR generator and parser.
We would like to avoid this burden while increasing expressibility of the language.
The goal of plugins is to allow IR extensions without changing the core IR.

\subsection{Plugin Syntax}
Plugins allow a circuit to refer to specific functionalities.
Those functionalities are defined in a document.
Only backends that have an implementation of a plugin can evaluate statements containing that plugin.

In the circuit's syntax, plugins are similar to functions except the function body is replaced by the plugin.
The declaration of a plugin function starts with the signature of a function followed by the use of a \texttt{@plugin} directive with plugin parameters that includes the plugin name, the operation name, and its generic parameters.
Invocation will remain the same as for functions.
A function bound to a plugin must be declared before its invocation.

The names of plugins used % (e.g. vector, matrix, ram\_write)
must be specified in the header. % before the `@begin` keyword.
This ensures that backends can easily check which plugins are used and reject the circuit if needed, prior to starting circuit evaluation.

 % Examples
 Here is an example use of the \texttt{vectors\_v1} plugin that provides a \texttt{mul} operation over ranges of wires.
\begin{lstlisting}[language=ir]
...
@plugin vectors_v1;
...
@begin
  ...
  // declare the function signature with a plugin body
  @function(vec_mul_4, @out: 0:4, @in: 0:4, 0:4)
      @plugin(vectors_v1, mul);
  ...
  // call the vec_mul_4 plugin function
  $8 ... $11 <- @call(vec_mul_4, $0 ... $3, $4 ... $7);
  ...
@end
\end{lstlisting}

Plugin operations are defined elsewhere, so the \texttt{@plugin} binding takes a list of plugin defined arguments after the required plugin and operation names.
Function signatures and their calls (\texttt{@call}) remain well-specified in the IR.
Each instantiation of a plugin operation % that's used in the circuit 
requires a separate function declaration and \texttt{@plugin} binding.
Plugin binding arguments consist of a comma separated sequence
of identifiers and numeric literals.
In practice, this enables plugins to specify parameters like fields, lengths, and other functionality.
Providing other functions' names as generic arguments can enable higher order operations like maps and folds. 
%
\begin{lstlisting}[language=ir]
/* ... */
@plugin vectors_v1;
@plugin iter_v0;
/* ... */
@begin
  /* ... */
  // Multiple instantiations of the same plugin (vector, mul)
  @function(vec_mul_4, @out: 0:4, @in: 0:4, 0:4)
      @plugin(vectors_v1, mul);
  @function(vec_mul_2, @out: 0:2, @in: 0:2, 0:2)
      @plugin(vectors_v1, mul);

  // Numeric parameter used as a circuit constant by the plugin
  @function(vec_double_4, @out: 0:4, @in: 0:4)
      @plugin(vectors_v1, mulc, 2);

  // Higher order map operation.
  @function(plus1_0, @out: 0:1, @in: 0:1) /* ... */ @end
  // An identifier parameter for the sub-function name,
  // A numeric parameter describing function arguments by their order
  // A numeric parameter for the iteration count
  @function(vec_plus1_4, @out: 0:4, @in: 0:4)
      @plugin(iter_v0, map, plus1_0, 0, 4);
  /* ... */
@end
\end{lstlisting}

Each plugin operation has a signature, which is defined as part of the standard for the plugin.
If the backend sees a \texttt{@plugin} for a known plugin, and the function signature doesn't match the expected signature of the operation it is being bound to, then the circuit is invalid. % the TA2 should reject the circuit with an error.
Further, the plugin's name must have been declared in the circuit header.
Either of these errors would make the circuit \textbf{poorly formed}.

Plugin operations may consume some public and private inputs. % instances and witnesses.
If a plugin operation consumes input from the standard public or private streams, its plugin binding
must contain the count of the number of consumed public or private inputs % instances and witnesses 
per field.
However, a plugin may be sophisticated enough to produce its own public or private inputs and consume them immediately, in which case input usage need not be declared.
%
Here is an example use of an \texttt{assert\_equal} plugin that checks that the five inputs are equal to the five next private inputs.
\begin{lstlisting}[language=ir]
/* ... */
@plugin assert_equal;
/* ... */
@begin
  /* ... */
  // declare the function signature with a plugin body
  @function(equal_to_private, @in: 0:5)
    @plugin(assert_equal, private, 0, 5, @private: 0:5);
  /* ... */
  // call the equal_to_private plugin
  @call(equal_to_private, $4 ... $8);
  /* ... */
@end
\end{lstlisting}

\subsection{Plugin Types}
%
For some plugins, it is useful to declare additional types that are distinct from ordinary field types.
Plugins can define new types by using the 
\texttt{@type @plugin(plugin\_id, type\_id, ...)} directive in the circuit header, which again takes a list of plugin-specified parameters. 
The first two parameters name the plugin, and a particular type within the plugin.
Subsequent parameters may be identifiers or numbers, and their semantics are defined by the plugin.
Types declared by a plugin can be manipulated via its plugin functions, or the plugin may overload built-in gates for its types.
Here is an example demonstrating how the \texttt{ram\_arith\_v1} plugin uses a plugin type to implement a buffer.

\begin{lstlisting}[language=ir]
version 2.0.0;
circuit;
@plugin ram_arith_v1;
// Wire type 0 is the field mod 127
@type field 127;
// Wire type 1 is the ram plugin type, with indexes and
// elements both drawn from field 0.
@type @plugin(ram\_arith\_v1, state, 0);

@begin
  // Declare RAM operations as abstract functions.
  // ram.init creates a buffer of fixed size and
  // initializes each element to the same input value.
  @function(ram_init, @out: 1:1, @in: 0:1) 
    @plugin(ram, init, 10);

  // ram.read takes an RAM buffer and an address
  // and returns the value at that address.
  @function(ram_read, @out: 0:1, @in: 1:1, 0:1)
    @plugin(ram_arith_v1, read);

  // ram_write takes a RAM buffer, an address, and
  // a new value and writes the value to the address.
  @function(ram_write, @in: 1:1, 0:1, 0:1)
    @plugin(ram, write);

  @function(assert_eq, @in: 0:1, 0:1)
    $2 <- @addc($0, <126>); // p-1
    $3 <- @add($1, $2);
    @assert_zero($3);
  @end

  // Initialize all elements to 0 (type 0) in a newly
  // allocated RAM buffer (type 1)
  $0 <- 0: <0>;
  $0 <- @call(ram_init, $0);

  // Write something to the address
  $1 <- @private_in(); // address
  $2 <- @private_in(); // value

  //             1:$0 RAM Buffer
  //                 0:$1 address wire
  //                     0:$2 value wire
  @call(ram_write, $0, $1, $2);

  // Read from the same address and check correctness.
  $3 <- @call(ram_read, $0, $1);
  @call(assert_eq, $2, $3);
@end
\end{lstlisting}

